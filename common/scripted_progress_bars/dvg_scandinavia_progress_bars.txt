dvg_scandinavia_kings_anger_progress_bar = {
	name = "dvg_scandinavia_kings_anger_progress_bar"
 	desc = "dvg_scandinavia_kings_anger_progress_bar"

 	default_bad = yes
	
	start_value = 0
	min_value = 0
	max_value = 100
	
	monthly_progress = {
		#Natural decay, adjust this for balancing, or remove it if its not needed.
		if = {
			limit = {
				exists = c:SCA
				c:SCA = ROOT
			}
			add = {
				desc = "kings_anger_natural_decay_tt"
				subtract = 1
			}
		}
		#Landowners approval
		if = {
			limit = {
				ig:ig_landowners = { is_powerful = yes }
				ig:ig_landowners = {
					ig_approval >= 10
				}				
			}
			add = {
				desc = "kings_anger_landowners_approval_tt"
				subtract = 0.2
			}
		}
		if = {
			limit = {
				ig:ig_landowners = { is_powerful = yes }
				ig:ig_landowners = {
					ig_approval >= 5
				}
				NOT = {
					ig:ig_landowners = {
						ig_approval >= 10
					}
				}
			}
			add = {
				desc = "kings_anger_landowners_approval_tt"
				subtract = 0.1
			}
		}
		if = {
			limit = {
				ig:ig_landowners = { is_powerful = yes }
				ig:ig_landowners = {
					ig_approval <= 0
				}
				NOT = {
					ig:ig_landowners = {
						ig_approval <= -5
					}
				}
			}
			add = {
				desc = "kings_anger_landowners_approval_tt"
				add = 0.25
			}
		}
		if = {
			limit = {
				ig:ig_landowners = { is_powerful = yes }
				ig:ig_landowners = {
					ig_approval <= -5
				}
				NOT = {
					ig:ig_landowners = {
						ig_approval <= -10
					}
				}
			}
			add = {
				desc = "kings_anger_landowners_approval_tt"
				add = 0.75
			}
		}
		if = { #idk man i feel like if you're heading towards a civil war, they're gonna be pretty angry
			limit = {
				ig:ig_landowners = { is_powerful = yes }
				ig:ig_landowners = {
					ig_approval <= -10
				}
			}
			add = {
				desc = "kings_anger_landowners_approval_tt"
				add = 2
			}
		}		
	}
}

dvg_scandinavia_highland_clearances_progress_bar = {
	name = "dvg_scandinavia_highland_clearances_progress_bar"
 	desc = "dvg_scandinavia_highland_clearances_progress_bar_desc"
	
 	default = yes
	
	start_value = 60
	min_value = 0
	max_value = 200

	monthly_progress = {
		if = {
			limit = {
				exists = c:SCA
				c:SCA = ROOT
			}
			add = {
				desc = "highland_clearance_gradual_progression_tt"
				add = 3
			}
		}
	}

}